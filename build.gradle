// gradle build script for html sanity check
// =========================================

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.0"
    }
}


plugins {
    id 'org.asciidoctor.convert' version '1.5.2'
    id "org.sonarqube" version "1.0"
}

group = 'org.aim42'
version = "0.9.3"

ext {

    srcDir = "$projectDir/src/docs"
    javaVersion = System.getProperty("java.version")
    currentDate = new Date().format("d. MMM yyyy")
}

println "=" * 50
print "building " + name
println ", version " + version
println "projectDir  : " + projectDir
println "buildDir    : " + buildDir
println "Java version: " + javaVersion
println "=" * 50


apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'java-gradle-plugin'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {


    localGroovy()
    testCompile("org.spockframework:spock-core:1.0-groovy-2.3")
            { exclude group: 'org.codehaus.groovy'}

    compile gradleApi()
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.3'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.3'

    testCompile 'junit:junit:4.11'

    // jsoup is our awesome html parser, see jsoup.org
    compile group: 'org.jsoup', name: 'jsoup', version: '1.8.1'

    // mockito needed to test string similarity
    testCompile "org.mockito:mockito-all:1.9.5"

} // dependencies



tasks.withType(JavaCompile) {
    sourceCompatibility = rootProject.sourceCompatibility
    targetCompatibility = rootProject.targetCompatibility
}
tasks.withType(GroovyCompile) {
    sourceCompatibility = rootProject.sourceCompatibility
    targetCompatibility = rootProject.targetCompatibility
}


// ========================================================
apply plugin: 'org.asciidoctor.convert'



asciidoctor {
    outputDir = file(buildDir)
    sourceDir = file(srcDir)
    sources {
        include "hsc_arc42.adoc",
                "index.adoc",
                "DevelopmentGuide.adoc"
    }

    attributes = [
            doctype             : 'book',
            icons               : 'font',
            toc                 : 'left',
            sectlink            : true,
            sectanchors         : true,
            numbered            : true,
            'source-highlighter': 'coderay',
            'javaVersion'       : "$javaVersion",
            'currentDate'       : "$currentDate"]

    resources {
        from("${srcDir}/images") {
            include '**'
        }
        into "./images"
    }

} // asciidoctor

// ==========================================================
// if you use htmlSanityCheck locally, publish with
// "gradle uploadToLocalMaven

apply plugin: 'maven-publish'


publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'org.aim42'
            artifactId project.name
            version project.version
            from components.java
        }

    }
}

// =========================================================

// determine test coverate
apply from: "gradle/coverage.gradle"

// calculate groovy metrics
apply from: "gradle/gmetrics.gradle"

// invoke sonar runner
apply plugin: "org.sonarqube"

sonarqube {
    properties {
        property "sonar.projectKey", "htmlSanityCheck"
        //property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
    }
}


// =========================================================

apply plugin: "com.gradle.plugin-publish"

pluginBundle {
    website = 'https://github.com/aim42/htmlSanityCheck'
    vcsUrl = 'https://github.com/aim42/htmlSanityCheck'
    description = 'Checks HTML files for broken links, missing images etc.'
    tags = ['checks', 'quality', 'documentation', 'html', 'asciidoctor', 'test']

    plugins {
        htmlSanityCheck {
            id = 'org.aim42.htmlSanityCheck'
            displayName = 'htmlSanityCheck plugin'

        }
    }
}



// =========================================================
task info(
        description: "info on path and objects"
) <<
        {
            println "project.name         : " + project.name
            println "project.version      : " + project.version
            println "project.path         : " + project.path
            println "projectDir           : " + projectDir
            println "groupId              : " + project.group
            println "buildDir             : " + buildDir
            println "targetCompatibility  : " + rootProject.targetCompatibility
            println "="*80
        }


/*
 * Copyright 2012-2015 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 *
 */
