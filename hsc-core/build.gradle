// gradle build script for subproject "hsc-core"

buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:0.6"
        classpath "nu.studer:gradle-credentials-plugin:1.0.1"

    }
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'nu.studer.credentials'
apply plugin: 'maven-publish'
apply plugin: 'java'


dependencies {

    // jsoup is our awesome html parser, see jsoup.org
    compile group: 'org.jsoup', name: 'jsoup', version: '1.8.1'

    // mockito needed to test string similarity
    testCompile "org.mockito:mockito-all:1.9.5"

}

jar {
    manifest {
        attributes 'Implementation-Title': 'HTML Sanity Check Core',
                'Implementation-Version': version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Gradle': gradle.gradleVersion
    }
}


def pomConfig = {
    name project.name
    description project.project_description
    url project.project_url
    inceptionYear '2014'

    scm { url project.project_scm }



    developers {
        developer {
            id 'gernotstarke'
            name 'Gernot Starke'
            url 'http://www.gernotstarke.de/'
            email 'gs@gernotstarke.de'
            roles { role 'Developer' }
        }
    }

    contributors {
        contributor {
            name 'Ralf Mueller'
            email 'ralf.d.mueller@gmail.com'
        }

    }
}

publishing {
    publications {
        mavenCustom(MavenPublication) {
            from components.java

            pom.withXml {
                asNode().children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = credentials.bintrayUserName
    key = credentials.bintrayApiKey
    publications = ['mavenCustom'] // see publications closure

    dryRun = false
    publish = true
    pkg {
        repo = "HtmlSanityCheck"
        name = 'hsc-core'
        desc = 'core package to perform semantic html checking'
        websiteUrl = 'https://github.com/aim42/htmlSanityCheck'
        issueTrackerUrl = 'https://github.com/aim42/htmlSanityCheck/issues'
        vcsUrl = 'https://github.com/aim42/htmlSanityCheck.git'
        licenses = ['Apache-2.0']
        labels = ['groovy', 'html', 'check']
        publicDownloadNumbers = true
        //Optional version descriptor
        version {
            name = version //Bintray logical version name
            desc = 'first public version of hsc-core'
        }
    }
}

// just for instructional purpose:
// -------------------------------

// read credentials at build time, assuming credentials have already been
// written to the file system through Gradle task before via
// gradle addCredentials --key bintrayUserName --value <username>

// uncomment the following lines to see the (locally) stored credentials

// println "Username        : " + credentials.bintrayUserName
// println "Bintray API Key : " + credentials.bintrayApiKey



