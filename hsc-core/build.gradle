// gradle build script for subproject "hsc-core"
println '(subproject hsc-core) initialization'
println "project.name: $project.name"

buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:0.6"
        classpath "nu.studer:gradle-credentials-plugin:1.0.1"

    }
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'nu.studer.credentials'
apply plugin: 'maven-publish'
apply plugin: 'java'


dependencies {

    // jsoup is our awesome html parser, see jsoup.org
    compile group: 'org.jsoup', name: 'jsoup', version: '1.8.1'

    // mockito needed to test string similarity
    testCompile "org.mockito:mockito-all:1.9.5"

}

jar {
    manifest {
        attributes 'Implementation-Title': 'HTML Sanity Check Core',
                'Implementation-Version': project.project_version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Gradle': gradle.gradleVersion
    }
}


def pomConfig = {
    name project.name
    description project.project_description
    url project.project_url
    inceptionYear project.project_inception_year

    scm { url project.project_scm }



    developers {
        developer {
            id project.project_author_id
            name project.project_vendor
            url 'http://www.gernotstarke.de/'
            email 'gs@gernotstarke.de'
            roles { role 'Developer' }
        }
    }

    contributors {
        contributor {
            name 'Ralf Mueller'
            email 'ralf.d.mueller@gmail.com'
        }

    }
}

publishing {
    publications {
        mavenCustom(MavenPublication) {
            groupId project.project_bintray_org
            artifactId "HtmlSanityCheck-core"
            version project.project_version
            from components.java

            pom.withXml {
                asNode().children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = credentials.bintrayUserName
    key = credentials.bintrayApiKey
    publications = ['mavenCustom'] // see publications closure

    dryRun = project.dryRun
    publish = project.publish

    pkg {
        repo = "HtmlSanityCheck"
        name = project.name //formerly 'hsc-core'
        desc = 'core package to perform semantic html checking'
        websiteUrl = project.project_url
        issueTrackerUrl = 'https://github.com/aim42/htmlSanityCheck/issues'
        vcsUrl = 'https://github.com/aim42/htmlSanityCheck.git'
        licenses = project.project_license
        labels = ['groovy', 'html', 'check']
        publicDownloadNumbers = true
        //Optional version descriptor
        version {
            name = project.project_version //Bintray logical version name
            desc = 'first public version of HtmlSanityCheck-core'
        }
    }
}

// just for instructional purpose:
// -------------------------------

// read credentials at build time, assuming credentials have already been
// written to the file system through Gradle task before via
// gradle addCredentials --key bintrayUserName --value <username>

// uncomment the following lines to see the (locally) stored credentials

// println "Username        : " + credentials.bintrayUserName
// println "Bintray API Key : " + credentials.bintrayApiKey



